group "org.geobon"
version "1.0.0"

wrapper {
    gradleVersion = "6.9.2"
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

buildscript {
    ext.kotlin_version = '1.7.10'
    ext.ktor_version = "1.5.4"
    ext.shadow_version = "6.1.0"

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
        classpath("com.github.jengelman.gradle.plugins:shadow:$shadow_version")
    }
}

apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "application"

mainClassName = "io.ktor.server.netty.DevelopmentEngine"

// Initialization order with shadow 2.0.1 and Gradle 6.9 is weird.
// See https://github.com/johnrengelman/shadow/issues/336#issuecomment-355402508
apply plugin: "com.github.johnrengelman.shadow"

sourceCompatibility = 1.8

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    archiveBaseName.set('kotlin-server')
    archiveClassifier.set('')
    archiveVersion.set('')
}

test {
    useJUnitPlatform()
    environment "SCRIPT_LOCATION", "${projectDir}/src/test/resources/scripts/"
    environment "PIPELINES_LOCATION", "${projectDir}/src/test/resources/pipelines/"
    environment "OUTPUT_LOCATION", "${projectDir}/src/test/resources/outputs/"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version")
    implementation('ch.qos.logback:logback-classic:1.4.0')
    implementation("io.ktor:ktor-auth:$ktor_version")
    implementation("io.ktor:ktor-gson:$ktor_version")
    implementation("io.ktor:ktor-locations:$ktor_version")
    //implementation("io.dropwizard.metrics:metrics-core:4.1.18")
    implementation("io.ktor:ktor-metrics:$ktor_version")
    implementation("io.ktor:ktor-server-netty:$ktor_version")

    // https://mvnrepository.com/artifact/org.json/json
    implementation group: 'org.json', name: 'json', version: '20220320'

    // https://mvnrepository.com/artifact/org.yaml/snakeyaml
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.30'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    testImplementation 'io.mockk:mockk:1.12.7'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4'
    testImplementation 'io.kotest:kotest-runner-junit5:5.4.2'
}
